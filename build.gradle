buildscript {
    repositories { mavenCentral() }
}

group = 'org.joeyb'
version = "0.0.1"

apply from: file('gradle/convention.gradle')
apply from: file('gradle/coverage.gradle')
apply from: file('gradle/check.gradle')
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.auto.service:auto-service:1.0-rc2'
    compile 'com.squareup:javapoet:1.0.0'
    compile 'org.inferred:freebuilder:1.0-rc7'

    testCompile 'com.google.testing.compile:compile-testing:0.6'
    testCompile 'com.google.truth:truth:0.26'
    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Freemapper'
                packaging 'jar'
                description 'An annotation processor for generating ResultSet mapper classes'
                url 'https://github.com/joeyb/freemapper'

                scm {
                    url 'scm:git@github.com:joeyb/freemapper.git'
                    connection 'scm:git@github.com:joeyb/freemapper.git'
                    developerConnection 'scm:git@github.com:joeyb/freemapper.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'joeyb'
                        name 'Joey Bratton'
                        email 'joey@joeyb.org'
                    }
                }
            }
        }
    }
}
